include_guard(GLOBAL)

message(STATUS "Using ${DEPENDENCY_VERSION} of JBIG library.")

configure_file(
	"${CMAKE_CURRENT_LIST_DIR}/CMakeLists.in.cmake"
	"${CMAKE_CURRENT_BINARY_DIR}/${DEPENDENCY}.cmake"
	@ONLY
	NEWLINE_STYLE UNIX
)

ExternalProject_Add("${DEPENDENCY}"
	${EP_BASE_LOCATION_ARGS}
	GIT_REPOSITORY "https://www.cl.cam.ac.uk/~mgk25/git/jbigkit"
	GIT_TAG "v${DEPENDENCY_VERSION}"
	GIT_SUBMODULES_RECURSE TRUE
	GIT_SHALLOW FALSE
	GIT_PROGRESS FALSE
	GIT_REMOTE_UPDATE_STRATEGY CHECKOUT
	PATCH_COMMAND
		"${CMAKE_COMMAND}"
		"-E"
		"copy_if_different"
		"${CMAKE_CURRENT_BINARY_DIR}/${DEPENDENCY}.cmake"
		"<SOURCE_DIR>/libjbig/CMakeLists.txt"
	${EP_CMAKE_DEFAULT_GENERATOR}
	CMAKE_ARGS
		${EP_CMAKE_DEFAULT_CONFIGURE_ARGS}
	CMAKE_CACHE_ARGS
		${EP_CMAKE_DEFAULT_CONFIGURE_CACHE_ARGS}
	SOURCE_SUBDIR libjbig
	CONFIGURE_HANDLED_BY_BUILD TRUE
	${EP_WITH_BUILD_EXTRA_ARGS}
	EXCLUDE_FROM_ALL TRUE
)

set(DEPENDENCY_ENABLED TRUE)
